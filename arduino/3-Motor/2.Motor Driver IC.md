#Using an L293D Motor Driver IC
In our previous project, we used a transistor to control the motor. In this project, we are going to use a very popular motor driver IC called an L293D. The advantage of using this chip is that we can control two motors at the same time, as well as control their direction. The chip can also be used to control a stepper motor, as we will find out in Project 28. There is also a pin-for-pin compatible chip available known as the SN754410, which can also be used and has a higher current rating. The IC has its own internal diodes, so we do not need one for this project

| parts |   |
|-------|---|
|DC Motor   | ![dc motor](https://cloud.githubusercontent.com/assets/3673943/3596515/c950444e-0cc5-11e4-9b3f-4078063025fd.jpg)|
|L293D or SN754410 Motor Driver IC  | ![motor driver ic](https://cloud.githubusercontent.com/assets/3673943/3596679/4d2cd348-0cc8-11e4-99f5-491878f1dff6.jpg) |
|10KΩ Potentiometer | ![10 k potentiometer](https://cloud.githubusercontent.com/assets/3673943/3596514/c94ffe80-0cc5-11e4-9cd8-4a0549e28668.jpg) |
|10KΩ Resistor  | ![10k resistor](https://cloud.githubusercontent.com/assets/3673943/3596092/7911c1ca-0cbf-11e4-92df-eb7f433b9511.jpg)|
|Heatsink   | ![heatsink](https://cloud.githubusercontent.com/assets/3673943/3596677/4d27d7e4-0cc8-11e4-83b7-42e36be83163.jpg)|
|2 x 0.1uF capacitors| ![capacitor](https://cloud.githubusercontent.com/assets/3673943/3596678/4d280610-0cc8-11e4-9ac2-dcca865c67bf.jpg) |
| Slider switch | ![slider](https://cloud.githubusercontent.com/assets/3673943/3596680/4d2ea5b0-0cc8-11e4-89c8-4e6abd60987a.jpg) |

####Connect It Up
First, make sure your Arduino is powered off by unplugging it from the USB cable. Now take the required parts and connect them up as in circuit below. Again, check the circuit thoroughly before powering it up. The L293D can get hot when in use, and therefore a heatsink is recommended. Glue the heatsink to the top of the chip using a special thermal epoxy glue, thermally conductive double-sided tape or use a clip-on style heat sink. The larger the heatsink, the better. Be warned that the temperature can get hot enough to melt the plastic on a breadboard or any wires touching it. Do not touch the heatsink as you may burn yourself and ensure you do not leave the circuit powered up and unattended in case it overheats. It may be prudent to use stripboard instead of a breadboard for this project to save damaging your breadboard due to heat.


####Enter the Code
Once you are satisfied that your circuit is connected up correctly, upload the code in Listing 5-2. Do not connect the external power supply at this stage.

```c
// Project 16 - Using an L293D Motor Driver IC
#define switchPin 2 // switch input
#define motorPin1 3 // L293D Input 1
#define motorPin2 4 // L293D Input 2
#define speedPin 9  // L293D enable pin 1
#define potPin 0    // Potentiometer on analog Pin 0
int Mspeed = 0;     // a variable to hold the current speed value

void setup() {
//set switch pin as INPUT
pinMode(switchPin, INPUT);

// set remaining pins as outputs
pinMode(motorPin1, OUTPUT);
pinMode(motorPin2, OUTPUT);
pinMode(speedPin, OUTPUT);
}

void loop() {
  Mspeed = analogRead(potPin)/4;   // read the speed value from the potentiometer
  analogWrite (speedPin, Mspeed);  // write speed to Enable 1 pin
  if (digitalRead(switchPin)) {    // If the switch is HIGH, rotate motor clockwise
    digitalWrite(motorPin1, LOW);  // set Input 1 of the L293D low
    digitalWrite(motorPin2, HIGH); // set Input 2 of the L293D high
  }
  else {  // if the switch is LOW, rotate motor anti-clockwise
    digitalWrite(motorPin1, HIGH); // set Input 1 of the L293D low
    digitalWrite(motorPin2, LOW);  // set Input 2 of the L293D high
  }
}
```

Once the code has finished uploading, set the potentiometer at its midpoint and plug in the external power supply. The motor will now rotate and you can adjust its speed by turning the potentiometer. To change direction of the motor, first set the speed to minimum, then flick the switch. The motor will now rotate in the opposite direction. Again be careful of that chip, as it will get 

####Code Overview
The code for this project is very simple. First we define the pins we are going to put to use on the Arduino.
```c
#define switchPin 2 // switch input
#define motorPin1 3 // L293D Input 1
#define motorPin2 4 // L293D Input 2
#define speedPin 9  // L293D enable pin 1
#define potPin 0    // Potentiometer on analog Pin 0
Then set an integer to hold the speed value read from the potentiometer.
int Mspeed = 0;  // a variable to hold the current speed value
```

In the setup function we then set the appropriate pins to either inputs or outputs.
```c
pinMode(switchPin, INPUT);

pinMode(motorPin1, OUTPUT);
pinMode(motorPin2, OUTPUT);
pinMode(speedPin, OUTPUT);
```

In the main loop we first read in the value from the potentiometer connected to analog pin 0 and store it in Mspeed.
Mspeed = analogRead(potPin)/4; // read the speed value from the potentiometer
Then we set the PWM value on PWM pin 9 to the appropriate speed.
analogWrite (speedPin, Mspeed); // write speed to Enable 1 pin
Then we have an if statement to decide if the value read in from the switch pin is either HIGH or LOW. If it is HIGH, then output 1 on the L293D is set to LOW and output 2 is set to HIGH. This will be the same as output 2 having a positive voltage and output 1 being ground, causing the motor to rotate in one direction.
```c
if (digitalRead(switchPin)) {      // If the switch is HIGH, rotate motor clockwise
    digitalWrite(motorPin1, LOW);  // set Input 1 of the L293D low
    digitalWrite(motorPin2, HIGH); // set Input 2 of the L293D high
  }
```

If the switch pin is LOW, then output 1 is set to HIGH and output 2 is set to LOW, reversing the direction of the motor.

```c
else {                           // if the switch is LOW, rotate motor anti-clockwise
  digitalWrite(motorPin1, HIGH); // set Input 1 of the L293D low
  digitalWrite(motorPin2, LOW);  // set Input 2 of the L293D high
}
```

The loop will now repeat, checking for a new speed value or a new direction chosen and setting the appropriate speed and direction pins. As you can see, using the motor driver IC is not at all as daunting as you might have thought at first. In fact, it has made your life a lot easier. Trying to recreate the above circuit and code without it would have been far more complex. Never be intimidated by ICs as they are usually a lot simpler than they first appear. A slow and careful read of their datasheets will reveal their secrets. Let’s see how the new components introduced in this project work.

####Using an L293D Motor Driver IC – Hardware Overview
The new component we have come across in Project 16 is the motor driver IC. This will be either the L293D or the SN754410, depending on what you have chosen (there are other chips available and a little research on the Internet will find other pin-compatible motor drivers). The L293D is recommended as the SN754410 can run hot and require a heat sink.
The L293D is what is known as a dual H-bridge. An H-bridge is a useful and simple electronic concept. You could make your own H-bridge using switches.

![h bridge switches](https://cloud.githubusercontent.com/assets/3673943/3596842/e48520c2-0cca-11e4-9a0d-3dcdb12a3aa5.jpg)

A motor is connected to four switches. The configuration is called an H-bridge because it resembles a letter H, with the load bridge as the center.

![changing motor direction - h bridge switches](https://cloud.githubusercontent.com/assets/3673943/3596838/e43233ee-0cca-11e4-98ba-5321b74e58c9.jpg)

On the left hand side, we have the top left and the bottom right switches closed. When closed, the current will flow across the motor from left to right and the motor will rotate. If we then open those switches and close the top right and bottom left switches instead, the current will flow across the motor in the opposite direction and hence cause it to rotate in the opposite direction. This is how an H-bridge works.

The motor driver IC is made up of 2 H-bridges, and instead of switches, it uses transistors. Just as the transistor in project 15 was used as a switch, the transistors inside the H-bridge switch on and off in the same configuration as in Figure 5-5 to make your motor rotate in either direction. The chip is a dual H-bridge as it has two H-bridges inside it; thus, you are able to control two motors and their directions at the same time.
If you wanted to, you could make your own H-bridge out of transistors and diodes and it would do the same job as the L293D. The L293D has the advantage of being tiny and having all those transistors and diodes packaged up inside a small space.

The L293D has two power supply pins in addition to the four ground pins. Pin 16 (Vcc1) is the supply voltage for the logic circuitry of the chip, and should be fed from the Arduino’s 5V supply. Pin 8 (Vcc2) is the supply voltage for the power circuitry of the chip (which actually drives the motor). The grounds of both supplies and all four ground pins of the chip should be connected together. The two H-bridges on the chip act independently, sharing only the two power supplies. Pin 1 serves as an enable for the H-bridge controlled by pins 2 and 7. Pin 9 serves as an enable for the H-bridge controlled by pins 10 and 15. If an enable input is connected to a logic low (or ground), all four switches of the corresponding H-bridge will be turned off (open). If an enable is a logic high (> 2.4 V) the pair of inputs for the corresponding H-bridge will control which of the 4 switches of H bridge are turned on, as follows:

H-Bridge 1-2 (enable controlled by pin 1)
![h bridge 1-2](https://cloud.githubusercontent.com/assets/3673943/3596839/e4510986-0cca-11e4-9b24-cf6fc6e2949f.JPG)

H-bridge 3-4  (enable controlled by pin9)
![h bridge 3-4](https://cloud.githubusercontent.com/assets/3673943/3596840/e470bf42-0cca-11e4-998c-6d2f5a9e5505.JPG)

The minimum supply voltage is 4.5 volts (applies to both supplies, Vcc1 and Vcc2). The maximum supply voltage for Vcc1 is 5.5 volts on the 754410 (higher on the L293D). The maximum supply voltage for Vcc2 (motor supply) is 36 volts. If Vcc2 is below 4.5 volts, strange things may happen on outputs, and chips may get hot.
Transistors are not perfect switches. When the transistors in the L293D are turned on, there will be a voltage drop across them, so the across the motor will usually be 1.5 to 2.5 volts less than Vcc2. (That 1.5 to 2.5 volts is what causes the chip to get warm.) The higher the motor current, the more power will be lost in the transistors. This is one of those facts of life. When dealing with more powerful motors, better driver chips and heat sinks are needed.

####EXERCISE
Instead of just one motor, try connecting up two, with two direction switches and potentiometers to control the direction and speed. The image below shows the pinouts of the chip.

![l293d pinouts](https://cloud.githubusercontent.com/assets/3673943/3596841/e48512d0-0cca-11e4-98b8-a829903e05bd.JPG)


